GRAMMAR:

script		::	statement*

statement	::	'@import' path
		|	<var> '=' term
		|	<id> arglist block?

path		::	<word>
		|	<string>

arglist		::	'(' args? ')'

args		::	arg ( ',' arg )*

arg		::	term
		|	<kwd> term

term		::	<string>
		|	<regexp>
		|	<var>
		|	<id> arglist

block		::	'{' script '}'


LEXICAL RULES:

<word>		::	[-+.*?:\/\w]+		(reasonable filenames & paths)

<string>	::	"(\\.|[^"\\])*"		(double quoted strings)
		|	'(\\.|[^'\\])*'		(single quoted strings)

<regexp>	::	\/(\\.|[^\/\\])*\/[imxouesn]*	(Ruby regexps)

<var>		::	\$\w+			('$' << the usual)

<id>		::	\$|[_a-zA-Z](\w|\$)*	(the usual, including '$')

<kwd>		::	[a-zA-Z_:][-\w:.]*:	(XML attributes << ':')

<comment>	::	#.*			(single line)
		|	'#[' thru matching ']#'	(multiline, matched/nested)